buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7"
    }
}
apply plugin: 'com.android.library'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.1.0"
group = "james.a.grant"

def siteUrl = 'https://github.com/beplaya/Wagon'
def gitUrl = 'https://github.com/beplaya/Wagon.git'



android {
    compileSdkVersion 22
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 22
        versionCode 1
        versionName version
    }

    buildTypes {

    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'wagon'
        userOrg = user
        licenses = ['MIT']
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        publish = true
    }
}


install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'wagon'
                url siteUrl
                licenses {
                    license {
                        name 'Creative Commons 1.0 Universal'
                        url 'http://creativecommons.org/publicdomain/zero/1.0/legalcode'
                    }
                }
                developers {
                    developer {
                        id 'beplaya'
                        name 'Grant James'
                        email 'beplaya@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:22.2.1'
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.robolectric:robolectric:3.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}